allprojects {
    apply plugin: 'idea'
    apply plugin: 'eclipse'

    group 'org.deepboof'
    version '0.2-SNAPSHOT'
}


subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'

    sourceCompatibility = 1.8

    repositories {
        mavenCentral()
        mavenLocal()

        maven {
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }

    dependencies {
        ['ip','io','visualize','feature','recognition'].each {String a ->
            compile('org.boofcv:'+a+':0.25-SNAPSHOT'){
                exclude group: 'org.deepboof' }}


        testCompile group: 'junit', name: 'junit', version: '4.12'
    }

    sourceSets {
        test {
            java {
                srcDir 'autogenerate/java'
            }
        }
    }

    test {
        ignoreFailures true
        reports.html.enabled = false
    }

    javadoc {
        configure(options) {
            failOnError = false
        }
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives javadocJar, sourcesJar
    }
}

idea {
    project {
        jdkName = '1.8 (64bit)'
        languageLevel = '1.8'
    }

    module {
        excludeDirs += file('.idea')
    }
}

// Disable the creation of jars for distribution.  If you don't do this it will crash
[':modules'].each {String a ->
    project(a) {
//        if( project.hasProperty('ossrhUsername') ) {
//            signArchives.enabled = false
//        }
        sourcesJar.enabled = false
        javadocJar.enabled = false
        jar.enabled = false
        uploadArchives.enabled = false
        install.enabled = false
    }
}